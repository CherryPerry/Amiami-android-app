apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: '../signing.gradle'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId 'ru.cherryperry.amiami'
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode 3_02_01
        versionName '3.2.1'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        def properties = new Properties()
        def file = file('fabric.properties')
        if (file.exists()) {
            file.withInputStream { stream ->
                properties.load(stream)
            }
            manifestPlaceholders = [fabricKey: properties['apiKey']]
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            versionNameSuffix '.debug'
            ext.enableCrashlytics = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':domain')
    implementation project(':data')

    implementation "com.android.support:support-v4:$compatVersion"
    implementation "com.android.support:appcompat-v7:$compatVersion"
    implementation "com.android.support:preference-v14:$compatVersion"
    implementation "com.android.support:recyclerview-v7:$compatVersion"
    implementation "com.android.support:design:$compatVersion"
    implementation "com.android.support:cardview-v7:$compatVersion"
    implementation "com.android.support:support-media-compat:$compatVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.browser:browser:1.2.0'

    implementation 'androidx.core:core-ktx:1.0.0'

    implementation('com.github.bumptech.glide:glide:4.8.0') {
        transitive = true
    }
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
    compileOnly "com.squareup.retrofit2:retrofit:$retoriftVersion"
    compileOnly "com.squareup.okhttp3:okhttp:$okhttpVersion"

    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"
    implementation "com.google.firebase:firebase-perf:$firebasePerformanceVersion"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation 'android.arch.lifecycle:extensions:1.1.1'
    kapt 'android.arch.lifecycle:compiler:1.1.1'

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
}

apply plugin: 'com.google.gms.google-services'