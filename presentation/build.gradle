import ru.cherryperry.dependencies.Deps

plugins {
    id 'app'
    id 'kotlin-android-setup'
    id 'kotlin-kapt'
    id 'io.fabric'
    id 'com.google.firebase.firebase-perf'
    id 'detekt'
    id 'dependencies'
}

android {
    defaultConfig {
        applicationId 'ru.cherryperry.amiami'
        versionCode 3_02_01
        versionName '3.2.1'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        def properties = new Properties()
        def file = file('fabric.properties')
        if (file.exists()) {
            file.withInputStream { stream ->
                properties.load(stream)
            }
            manifestPlaceholders = [fabricKey: properties['apiKey']]
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
    }
}

dependencies {
    implementation project(':domain')
    implementation project(':data')

    implementation "com.android.support:support-v4:$compatVersion"
    implementation "com.android.support:appcompat-v7:$compatVersion"
    implementation "com.android.support:preference-v14:$compatVersion"
    implementation "com.android.support:recyclerview-v7:$compatVersion"
    implementation "com.android.support:design:$compatVersion"
    implementation "com.android.support:cardview-v7:$compatVersion"
    implementation "com.android.support:support-media-compat:$compatVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'
    implementation Deps.androidx.browser

    implementation Deps.androidx.core

    implementation Deps.glide.library
    kapt Deps.glide.compiler

    compileOnly Deps.retrofit.library
    compileOnly Deps.okhttp.library

    implementation Deps.firebase.core
    implementation Deps.firebase.messaging
    implementation Deps.firebase.perf

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    implementation Deps.dagger.android
    implementation Deps.dagger.androidSupport
    kapt Deps.dagger.compiler
    kapt Deps.dagger.androidAnnotationProcessor

    implementation Deps.androidx.lifecycle.extensions
    kapt Deps.androidx.lifecycle.compiler

    testImplementation Deps.junit.library
    testImplementation Deps.robolectric.library
}

apply plugin: 'com.google.gms.google-services'
