apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: '../chrome-tabs.gradle'
apply from: '../signing.gradle'
apply plugin: 'io.fabric'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId 'ru.cherryperry.amiami'
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode 30000
        versionName '3.0.0'
        vectorDrawables.useSupportLibrary = true

        def properties = new Properties()
        def file = file('fabric.properties')
        if (file.exists()) {
            file.withInputStream { stream ->
                properties.load(stream)
            }
            manifestPlaceholders = [fabricKey: properties['apiKey']]
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            versionNameSuffix '.debug'
            ext.enableCrashlytics = false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    implementation project(':domain')
    implementation project(':data')

    implementation "com.android.support:appcompat-v7:$compatVersion"
    implementation "com.android.support:preference-v14:$compatVersion"
    implementation "com.android.support:recyclerview-v7:$compatVersion"
    implementation "com.android.support:design:$compatVersion"
    implementation "com.android.support:customtabs:$compatVersion"
    implementation "com.android.support:cardview-v7:$compatVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation "io.reactivex:rxjava:$rxVersion"

    implementation 'androidx.core:core-ktx:0.3'

    implementation 'com.github.bumptech.glide:glide:4.7.1'
    kapt 'com.github.bumptech.glide:compiler:4.7.1'
    implementation "com.squareup.retrofit2:retrofit:$retoriftVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-messaging:17.1.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }

    implementation 'com.jakewharton.rxbinding:rxbinding:1.0.1'

    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    def lifecycle_version = '1.1.1'
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    testImplementation "junit:junit:$junitVersion"
    testImplementation 'org.robolectric:robolectric:3.8'
}

apply plugin: 'com.google.gms.google-services'